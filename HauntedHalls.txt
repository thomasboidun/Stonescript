var PallasA1SF = 69
var PallasA1HF = 70
var PallasA2SF = 20
var PallasA2HF = 21

// Animation Cancel
func AnimationCancel()
  var isTwoHanded = !item.left
  var leftItem = item.left
  var rightItem = item.right

  ?item.right.state = 3
    equipR shield *0 +0
  ?item.left.state = 3
    equipL wand *0 +0

  ?isTwoHanded
    ?rightItem
      equip @rightItem@
    :?leftItem
      equip @leftItem@
  :
    ?rightItem
      equipR @rightItem@
    ?leftItem
      equipL @leftItem@

AnimationCancel()

// Equip Functions
func EquipDashing()
  equipL trisk
  equipR dashing *10


func EquipWands()
  equipL vigor wand dL *10 +15
  equipR vigor wand D *10 +15


func EquipSwords()
  equipL vigor sword dL *10 +15
  equipR vigor sword D *10 +15


// Fighting Foes
? loc = haunted_halls & loc ! boss
  ? foe.distance > 10
    EquipDashing()
    ? item.CanActivate() &
    ^ item.GetCooldown(＂quarterstaff＂) <= 0
      equip quarterstaff
      ? item.CanActivate(＂quarterstaff＂)
        activate R
  ? foe.distance <= 10
    EquipWands()
    ? foe = boss // Mini Boss
      EquipSwords()

// Fighting Boss
? loc = haunted_halls & foe = Pallas
  ? foe = phase1
    ? foe.distance > 10
      EquipDashing()
    :
      EquipSwords()
    ? foe.state = 32 &
    ^ foe.time >= PallasA1SF
      equipR vigor shield ah

  ? foe = phase2    
    ? foe.count > 1
      EquipWands()
    :
      ? foe.distance > 10 &
      ^ foe.time ! 32 & foe.time ! 33
        EquipDashing()
      :
        EquipSwords()

// Pickup 
? pickup
  equipL trisk
  equipR star

// Display Loc Data
func TreasureLeft()
  var tCount = item.getTreasureCount()
  var tLimit = item.getTreasureLimit()
  
  ? tCount < 10
    return ＂00＂ + tCount + ＂/＂ + tLimit
  :? tCount < 100
    return ＂0＂ + tCount + ＂/＂ + tLimit
  :
    return tCount + ＂/＂ + tLimit


func TimeLeft()
  var tCount = item.getTreasureCount()
  var tLimit = item.getTreasureLimit()
  var tLeft = tLimit - tCount
  return (tLeft * loc.averageTime) - totaltime


func FormatTime(frames)
  var s = frames / 30
  var m = s / 60
  var h = m / 60

  s = s % 60
  m = m % 60

  ? h < 0
    h = 0
  ? m < 0
    m = 0
  ? s < 0
    s = 0

  ? h < 10
    h = ＂0＂ + h
  ? m < 10
    m = ＂0＂ + m
  ? s < 10
    s = ＂0＂ + s

  return h + ＂:＂ + m + ＂:＂ + s


func DisplayLocData()
  var tr_lft = TreasureLeft() + ＂ ＂
  var ti_lft = FormatTime(TimeLeft())
  var avg_ti = FormatTime(loc.averageTime)
  var bst_ti = FormatTime(loc.bestTime)
  var ttl_ti = FormatTime(totaltime)

  >╔══════════════════════╗\n
  ^║ Treasures │ @tr_lft@ ║\n
  ^║ Time left │ @ti_lft@ ║\n
  ^║───────────┼──────────║\n
  ^║ Avg. time │ @avg_ti@ ║\n
  ^║ Best time │ @bst_ti@ ║\n
  ^║ Ttl. time │ @ttl_ti@ ║\n
  ^╚══════════════════════╝


DisplayLocData()
