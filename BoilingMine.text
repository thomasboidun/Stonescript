func AnimationCancel()
  var isTwoHanded = !item.left
  var leftItem = item.left
  var rightItem = item.right

  ?item.right.state = 3
    equipR shield *0 +0
  ?item.left.state = 3
    equipL wand *0 +0

  ?isTwoHanded
    ?rightItem
      equip @rightItem@
    :?leftItem
      equip @leftItem@
  :
    ?rightItem
      equipR @rightItem@
    ?leftItem
      equipL @leftItem@

AnimationCancel()

func EquipTrisk()
  equipL trisk
  equipR dashing

func EquipWands()
  equipL aether wand dU *10
  equipR aether wand D *10

func EquipHammers()
  equipL aether hammer aU
  equipR aether hammer D

func EquipSwords()
  equipL aether sword dU
  equipR aether sword D

var BLADE_DELAY = 22
func ActivateBladeOfTheFallenGod()
  screen.Next()
  BLADE_DELAY--
  ? BLADE_DELAY <= 0
    equip blade
    activate R
    screen.ResetOffset()
    BLADE_DELAY = 22

? loc = mine
  ? foe.distance > 20
    EquipTrisk()

  ? foe.distance <= 20
    EquipWands()

  ? foe.distance <= 15 &
  ^ foe.count >= 5 &
  ^ item.GetCooldown("blade") <= 0
    ActivateBladeOfTheFallenGod()

  // Mini Boss
  ? foe = explode
    EquipTrisk()
    ? foe.distance <= 10
      equipR mind
  
  // Invoke Void Weaver
  ? summon.GetId() ! "voidweaver" &
  ^ item.CanActivate()
    equipR aether talisman
    activate R

  // Boss
  ? foe = Bronze Guardian
    ? foe.distance >= 20
      ? foe.state = 33
        EquipTrisk()
      :
        EquipWands()

    ? foe.distance <= 10
      ? foe.armor > 0
        EquipHammers()
      :
        EquipSwords()

      // Use Potion
      ? item.potion = empty | item.potion = auto
        brew wood + bronze // Berzerk Potion
        activate potion

    ? foe.state = 32 & foe.time >= 30
      ? item.GetCooldown("mask") >= 0
        equipR mask
        activate R
      :? item.GetCooldown("mind") >= 0
        equipR mind
      :
        equipR vigor shield *10

    // Invoke Cinder Wisp
    ? summon.GetId() ! "cinderwisp" &
    ^ item.CanActivate()
      equipR fire talisman
      activate R
    