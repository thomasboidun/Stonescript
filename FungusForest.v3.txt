// Angry Shroom
var ShroomPC  // Punch Count
var ShroomPCL // Punch Count Limit
var ShroomPSF // Punch Starting Frames
var ShroomPHF // Punch Hit Frame
var ShroomPRF // Punch Recovery Frames
var ShroomSC  // Shoot Count
var ShroomSCL // Shoot Count Limit
var ShroomSSF // Shoot Starting Frames
var ShroomSHF // Shoot Hit Frame
var ShroomSRF // Shoot Recovery Frames

// Morel
var MorelASF = 29 // Atk Starting Frames
var MorelAHF = 30 // Atk Hit Frame
var MorelARF = 30 // Atk Recovery Frames

? loc = fungus_forest
  // At the beginning
  ? loc.begin | loc.loop
    ShroomPC = 0
    ShroomPCL = 1
    ShroomPSF = 58
    ShroomPHF = 59
    ShroomPRF = 39
    ShroomSC = 0
    ShroomSCL = 3
    ShroomSSF = 6
    ShroomSHF = 7
    ShroomSRF = 7
    ? loc.stars >= 4
      ShroomPSF = 48
      ShroomPHF = 49
      ShroomPRF = 29
      ShroomSCL = 5
    ? loc.stars >= 5 & loc.stars <= 10
      ShroomSSF = 8
      ShroomSHF = 9
      ShroomSRF = 9

  ? foe.distance > 30
    equipL triskelion
    equipR poison shield ap
  ? foe.distance <= 30 & foe.distance > 23 &
  ^ item.CanActivate() &
  ^ item.GetCooldown(＂quarterstaff＂) <= 0
    equip quarterstaff
    ? item.CanActivate(＂quarterstaff＂)
      activate R
  ? foe.distance <= 23 & foe.distance >= 11
    equipL triskelion
    equipR dashing shield
  ? foe.distance <= 10
    ? foe.GetCount(23) > 1
      equipL poison long sword D
      equipR poison long sword dP
    :
      ? foe.armor
        equipL poison hammer D
        equipR poison hammer dP
      :
        equipL poison sword D -long
        equipR vigor shield shield

    // Mini Boss
    ? foe = snail & foe = boss &
    ^ foe.distance < 11
      equipL poison hammer D
      equipR poison hammer dP

    // Mr. Puff
    ? foe = explode
      equipL poison long sword D
      equipR poison long sword dP
      // Dodging explosion
      ? foe.distance <= 4 &
      ^ item.GetCooldown(＂mind＂) <= 0
        equipL triskelion
        equipR mind

  // Boss
  ? foe = Angry Shroom
    // On walk frames
    ? foe.state = 2
      >Walk

    // On punch starting frames
    ? foe.state = 32 & 
    ^ foe.time <= ShroomPSF & 
    ^ ShroomPC < ShroomPCL &
    ^ ShroomSC = 0
      >Punch starting frames
      equipL hammer D
      equipR hammer dP
      ? foe.time = ShroomPSF &
      ^ item.GetCooldown(＂mind＂) <= 0
        equipL mind
        equipR vigor shield ah
      ? foe.time = ShroomPSF &
      ^ item.GetCooldown(＂mind＂) > 0
        equipL poison hammer dP
        equipR vigor shield ah

    // On punch hit frame
    ? foe.state = 32 &
    ^ foe.time = ShroomPHF &
    ^ ShroomPC < ShroomPCL &
    ^ ShroomSC = 0
      >Punch hit frame
      ShroomPC++
      equipL poison hammer D
      equipR vigor shield ah

    // On punch recovery frames
    ? foe.state = 33 & 
    ^ foe.time <= ShroomPRF &
    ^ ShroomPC <= ShroomPCL &
    ^ ShroomSC = 0
      >Punch recovery frames
      equipL poison hammer D
      equipR poison hammer dP

    // On shoot starting frames
    ? foe.state = 32 &
    ^ foe.time <= ShroomSSF &
    ^ ShroomPC = ShroomPCL &
    ^ ShroomSC < ShroomSCL
      >Shoot starting frames
      equipL poison hammer D
      equipR vigor shield ah

    // On shoot hit frame
    ? foe.state = 32 &
    ^ foe.time = ShroomSHF &
    ^ ShroomPC = ShroomPCL &
    ^ ShroomSC < ShroomSCL
      >Shoot hit frame
      ShroomSC++
      equipL poison hammer D
      equipR vigor shield ah

    // On shoot recovery frames
    ? foe.state = 33 &
    ^ foe.time <= ShroomSRF &
    ^ ShroomPC = ShroomPCL &
    ^ ShroomSC <= ShroomSCL
      >Shoot recovery frames
      equipL poison hammer D
      equipR poison hammer dP

    // Reset counter
    ? foe.state = 33 &
    ^ foe.time = ShroomSRF &
    ^ ShroomPC = ShroomPCL &
    ^ ShroomSC = ShroomSCL
      ShroomPC = 0
      ShroomSC = 0

    // As soon as possible
    ? foe.distance > 10 &
    ^ !(foe.state = 32 &
    ^ foe.time = ShroomPHF)
      equipL triskelion
      equipR dashing

    ? !foe.armor
      equipL poison sword D -long
      equipR vigor shield ah
    
  ? foe = Morel
    ? foe.armor
      equipL poison hammer D
      equipR poison hammer dP
    :
      equipL poison sword D -long
      equipR poison long sword D
    ? hp < maxhp
      equipR vigor shield ah
    // Before Morel's punch hit frame
    ? foe.state = 32 & 
    ^ foe.time = MorelASF
      equipL mind
      equipR vigor shield ah
    ? foe.distance > 10 &
    ^ !(foe.state = 32 & foe.time = MorelAHF)
      equipL triskelion
      equipR dashing

  ? foe = Enoki
    ? foe.distance > 15
      equipL triskelion
      equipR vigor shield ah
    :? foe.distance <= 15 & foe.distance >= 11
      equipL poison sword D -long
      equipR dashing shield
    :
      equipL poison sword D -long
      equipR vigor shield ah
      ? hp = maxhp
        equipR poison long sword D

func FormatTime(frames)
  var s = frames / 30
  var m = s / 60
  var h = m / 60

  s = s % 60
  m = m % 60

  ? h < 0
    h = 0
  ? m < 0
    m = 0
  ? s < 0
    s = 0

  ? h < 10
    h = ＂0＂ + h
  ? m < 10
    m = ＂0＂ + m
  ? s < 10
    s = ＂0＂ + s

  return h + ＂:＂ + m + ＂:＂ + s

func TimeLeft()
  var tCount = item.getTreasureCount()
  var tLimit = item.getTreasureLimit()
  var tLeft = tLimit - tCount
  return (tLeft * loc.averageTime) - totaltime

func TreasureLeft()
  var tCount = item.getTreasureCount()
  var tLimit = item.getTreasureLimit()
  return tCount + ＂/＂ + tLimit

>Treasures: @TreasureLeft()@\n
^Time left: @FormatTime(TimeLeft())@\n
^To. time : @FormatTime(totaltime)@\n
^Best time: @FormatTime(loc.bestTime)@\n
^Av. time : @FormatTime(loc.averageTime)@\n
