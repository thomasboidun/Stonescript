disable hud r

var voidweaverIsInvoked

// Angry Shroom
var punchCounter
var punchCounterLimit
var punchStartingFrames
var punchHitFrame
var punchRecoveryFrames
var shootCounter
var shootCounterLimit
var shootStartingFrames
var shootHitFrame
var shootRecoveryFrames


// Morel
var MorelASF = 29 // Atk Starting Frames
var MorelAHF = 30 // Atk Hit Frame
var MorelARF = 30 // Atk Recovery Frames
var count = 0

? loc.begin | loc.loop
  // Set initial values
  voidweaverIsInvoked = false
  ? loc = fungus_forest
    punchCounter = 0
    punchCounterLimit = 1
    punchStartingFrames = 58
    punchHitFrame = 59
    punchRecoveryFrames = 39
    shootCounter = 0
    shootCounterLimit = 3
    shootStartingFrames = 6
    shootHitFrame = 7
    shootRecoveryFrames = 7
    ? loc.stars >= 4
      punchStartingFrames = 48
      punchHitFrame = 49
      punchRecoveryFrames = 29
      shootCounterLimit = 5
    ? loc.stars >= 5 & loc.stars <= 10
      shootStartingFrames = 8
      shootHitFrame = 9
      shootRecoveryFrames = 9

? hp < (maxhp / 3)
  activate potion

? !voidweaverIsInvoked
  // Invoke Voidweaver
  equipL aether talisman
  equipR triskelion
  ? item.CanActivate(＂aether_talisman＂)
    activate L
  ? item.CanActivate(＂voidweaver＂)
    voidweaverIsInvoked = true

? voidweaverIsInvoked
  // Walk faster
  equipL triskelion
  equipR compound
  
  ? loc = fungus_forest
    equipL triskelion
    equipR poison shield ap 

    ? foe.distance <= 15 & foe.distance >= 11
      equipL triskelion
      equipR dashing

    ? foe.distance <= 10
      equipL poison big sword D
      equipR poison big sword dP
      ? foe.GetCount(10) <= 1 &
      ^ foe.armor
        equipL poison hammer D
        equipR poison hammer dP

    ? foe = snail & foe = boss
      ? foe.distance <= 10
        equipL poison hammer D
        equipR poison hammer dP

    // Dodging Mr.Puff's explosion
    ? foe = explode
      equipL poison big sword D
      equipR poison big sword dP
      ? foe.distance <= 4 & 
      ^ item.GetCooldown(＂mind＂) <= 0
        equipL mind

    ? foe = Angry Shroom
      // On walk frames
      ? foe.state = 2
        >Walk

      // On punch starting frames
      ? foe.state = 32 & 
      ^ foe.time <= punchStartingFrames & 
      ^ punchCounter < punchCounterLimit &
      ^ shootCounter = 0
        >Punch starting frames
        equipL hammer D
        equipR hammer dP

        ? foe.time = punchStartingFrames &
        ^ item.GetCooldown(＂mind＂) <= 0
          equipL mind
          equipR vigor shield ah

        ? foe.time = punchStartingFrames &
        ^ item.GetCooldown(＂mind＂) > 0
          equipL poison hammer dP
          equipR vigor shield ah

      // On punch hit frame
      ? foe.state = 32 &
      ^ foe.time = punchHitFrame &
      ^ punchCounter < punchCounterLimit &
      ^ shootCounter = 0
        >Punch hit frame
        punchCounter++
        equipL poison hammer D
        equipR vigor shield ah

      // On punch recovery frames
      ? foe.state = 33 & 
      ^ foe.time <= punchRecoveryFrames &
      ^ punchCounter <= punchCounterLimit &
      ^ shootCounter = 0
        >Punch recovery frames
        equipL poison hammer D
        equipR poison hammer dP

      // On shoot starting frames
      ? foe.state = 32 &
      ^ foe.time <= shootStartingFrames &
      ^ punchCounter = punchCounterLimit &
      ^ shootCounter < shootCounterLimit
        >Shoot starting frames
        equipL poison hammer D
        equipR vigor shield ah

      // On shoot hit frame
      ? foe.state = 32 &
      ^ foe.time = shootHitFrame &
      ^ punchCounter = punchCounterLimit &
      ^ shootCounter < shootCounterLimit
        >Shoot hit frame
        shootCounter++
        equipL poison hammer D
        equipR vigor shield ah

      // On shoot recovery frames
      ? foe.state = 33 &
      ^ foe.time <= shootRecoveryFrames &
      ^ punchCounter = punchCounterLimit &
      ^ shootCounter <= shootCounterLimit
        >Shoot recovery frames
        equipL poison hammer D
        equipR vigor shield ah

      // Reset counter
      ? foe.state = 33 &
      ^ foe.time = shootRecoveryFrames &
      ^ punchCounter = punchCounterLimit &
      ^ shootCounter = shootCounterLimit
        punchCounter = 0
        shootCounter = 0

      // As soon as possible
      ? foe.distance > 10 &
      ^ !(foe.state = 32 &
      ^ foe.time = punchHitFrame)
        equipL triskelion
        equipR dashing

      ? !foe.armor
        equipL poison sword D -long
        equipR vigor shield ah
    
    ? foe = Morel
      equipL poison sword D -long
      equipR vigor shield ah

      ? hp = maxhp
        equipR poison long D

      ? foe.armor
        equipL poison hammer D
        ? hp = maxhp
          equipR poison hammer dP

      ? foe.state = 32 & 
      ^ foe.time = MorelASF
        equipL mind
        equipR vigor shield ah

      ? foe.distance > 10 &
      ^ !(foe.state = 32 & foe.time = MorelAHF)
        equipL triskelion
        equipR dashing

    ? foe = Enoki
      equipL poison sword D -long
      equipR vigor shield ah

      ? hp = maxhp
        equipR poison long D

      ? foe.distance > 17
        equipL triskelion

      ? foe.distance > 5 &
      ^ foe.distance <= 17
        equipR dashing
        activate R

  ? loc = crypt
    equipR vigor shield ah

    ? foe.distance <= 23
      equipL vigor wand dL
      equipR vigor wand D

      ? foe ! immune_to_physical
        equipL vigor sword dL
        equipR vigor shield ah

        ? item.getCooldown(＂quarterstaff＂) <= 0
        ^ | item.getCooldown(＂quarterstaff＂) >
        ^ 190
          equip quarterstaff
          ? foe.distance <= 11
            activate R
        :
          ? foe.distance > 5
            equipR dashing

    ? foe = Pallas & foe.distance <= 5
      equipL vigor sword dL
      equipR vigor sword D

  ? loc = icy_ridge
    ? foe.distance <= 23
      equip repeating
      ? foe.distance <= 5
        equipL poison sword D
        equipR vigor shield ah
        ? foe.armor
          equipL poison hammer D
      ? hp < maxhp
        equipL vigor wand dL
        equipR vigor shield ah
        ? foe.distance <= 5
          equipL vigor sword dL
 
  ? pickup.distance <= 10
    equipL star
    equipR triskelion


func framesToTime(frames)
  var s = frames / 30
  var m = s / 60
  var h = m / 60

  s = s % 60
  m = m % 60

  ? h < 0
    h = 0
  ? m < 0
    m = 0
  ? s < 0
    s = 0

  ? h < 10
    h = ＂0＂ + h
  ? m < 10
    m = ＂0＂ + m
  ? s < 10
    s = ＂0＂ + s

  return h+＂:＂+m+＂:＂+s


func getTimeLeft()
  var count = item.getTreasureCount()
  var limit = item.getTreasureLimit()
  var treasureLeft = limit - count
  var averageTime = loc.averageTime
  return (treasureLeft * averageTime) - 
  ^ totaltime

func getTreasureCount()
  var count = item.getTreasureCount()
  var limit = item.getTreasureLimit()
  return count + ＂/＂ + limit

var timeLeft
timeLeft = getTimeLeft()


>Treasures: @getTreasureCount()@\n
^Time left: @framesToTime(timeLeft)@\n
^To. time : @framesToTime(totaltime)@\n
^Best time: @framesToTime(loc.bestTime)@\n
^Av. time : @framesToTime(loc.averageTime)@\n
^Mind cd. : @item.GetCooldown(＂mind＂)@\n
^@foe.GetCount(10)@\n
^@foe.distance@
